#!/usr/bin/env perl

use strict;
use warnings;

use autodie;

use IO::All;
use Tie::IxHash;
use List::Util 'shuffle';

tie my %tree, 'Tie::IxHash';
my $tree = \%tree;
my @stack = ($tree);

for (io->file($ENV{LB_NOTES})->slurp) {
  my ($depth, $msg) = m/^(\t*)(.+)$/;

  pop @stack while $#stack > length $depth;
  tie my %itree, 'Tie::IxHash';
  push @stack, ( $stack[-1]{$msg} = \%itree );
}

sub show {
  my ($hash, $simplify, $fh, $offset) = @_;

  $fh ||= \*STDOUT;
  $offset ||= 0;

  for my $key (keys %$hash) {
    my $out = "$key\n";
    $out =~ s/\[_\]\s*// if $simplify;
    print $fh ("\t" x $offset) . $out;
    show($hash->{$key}, $simplify, $fh, $offset + 1) if $hash->{$key} && %{ $hash->{$key} };
  }
}

sub update {
  open my $fh, '>', "$ENV{LB_NOTES}.tmp";
  show(shift, 0, $fh);
  close $fh;
  rename "$ENV{LB_NOTES}.tmp", $ENV{LB_NOTES};
}

sub dig_random {
  my @path = @_;


  my $inner_tree = $tree;
  $inner_tree = $inner_tree->{$_} for @path;

  my ($choice) = shuffle(keys %$inner_tree);

  $inner_tree->{$choice =~ s/\[_\]/[X]/r } =
    delete $inner_tree->{$choice};

  update($tree);

  show({ $choice => $inner_tree->{$choice} }, 1);
}

chomp(my $in = do { local $/; <STDIN> });
$in =~ s/^\s*//;
$in =~ s/\s*$//;

if ($in =~ m/^inspire\s+me$/i) {
  dig_random('Inspiration')
} elsif ($in =~ m/^beer\s+me\s+a\s+video$/i) {
  dig_random('Videos')
}

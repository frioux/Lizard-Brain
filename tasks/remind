#!/usr/bin/env perl

use strict;
use warnings;

use Capture::Tiny 'capture_merged';
use Data::GUID 'guid_hex';
use DBI;
use Process::Status;

my $body = do { local $/; <STDIN> };

die "$body does not start with remind me to\n"
  unless $body =~ m/^remind me(?: to)? (.+) (at|in) (.+)$/i;

my $msg = $1;
my $mode = $2;
my $timespec = $3;

my $dbh = DBI->connect($ENV{LB_DSN});
my $id = lc guid_hex();
$id =~ s/^0x//;
$ENV{LB_REMINDER_ID} = $id;

$dbh->do(
  'INSERT INTO reminders (id, message) VALUES (?, ?)',
  undef, $id, $msg,
);

$timespec = "now + $timespec"
  if $mode eq 'in';

# TODO: exit non-zero if the command does
my $out = capture_merged {
  open(my $ph, '|-', 'at', $timespec);
  print $ph 'bin/lb-set-reminder-by-id | bin/action-pushover';
};

$out =~ s(warning: commands will be executed using /bin/sh\n)();

print $out
